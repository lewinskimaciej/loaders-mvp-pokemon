apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.example.mvp_pokemon"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        jackOptions {
            enabled false
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

// Dependency versions are defined in the top level build.gradle file. This helps keeping track of
// all version in a single place. This improves readability and helps managing project complexity.
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //
    // Android support
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:support-v4:$rootProject.ext.supportLibraryVersion"

    //
    // Network
    compile "com.squareup.okhttp3:okhttp:$rootProject.ext.okHttpVersion"
    compile "com.squareup.okhttp3:okhttp-urlconnection:$rootProject.ext.okHttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okHttpLoggerVersion"
    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"
    compile "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$rootProject.ext.retrofit2RxJava2AdapterVersion"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"
                                                                                                            
    //
    // ReactiveX
    compile "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxAndroidVersion"
    compile "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxJava2Version"
                                                                                                          
    //
    // ButterKnife
    compile "com.jakewharton:butterknife:$rootProject.ext.butterKnifeVersion"
    apt "com.jakewharton:butterknife-compiler:$rootProject.ext.butterKnifeVersion"
                                                                                                          
    //
    // Dagger 2
    compile "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
    apt  "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
    provided "javax.annotation:jsr250-api:$rootProject.ext.jsr250Version"
                                                                                                          
    //
    // JobQueue
    compile "com.birbit:android-priority-jobqueue:$rootProject.ext.jobQueueVersion"
                                                                                                          
    //
    // Gson
    compile "com.google.code.gson:gson:$rootProject.ext.gsonVersion"

    //
    // JodaTime
    compile "joda-time:joda-time:$rootProject.ext.jodaTimeVersion"    
                                                                                                          
    //
    // Logging
    compile "com.jakewharton.timber:timber:$rootProject.ext.timberVersion"
                                                                                                          
    //
    // Espresso idling resource
    compile "com.android.support.test.espresso:espresso-idling-resource:$rootProject.ext.espressoVersion"

    //
    // Glide
    compile "com.github.bumptech.glide:glide:$rootProject.ext.glideVersion"

    //
    // Requery
    compile "io.requery:requery:$rootProject.ext.requeryVersion"
    compile "io.requery:requery-android:$rootProject.ext.requeryVersion"
    apt "io.requery:requery-processor:$rootProject.ext.requeryVersion"

    ////////////////////////////////////////////////////////////////////////////////////////////////
    //                                                                                            //
    //                                       TESTING STUFF                                        //
    //                                                                                            //
    ////////////////////////////////////////////////////////////////////////////////////////////////
                                                                                                          
    //
    // Dependencies for local unit tests
    testCompile "junit:junit:$rootProject.ext.junitVersion"
    testCompile "org.mockito:mockito-all:$rootProject.ext.mockitoVersion"
    testCompile "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
                                                                                                          
    //
    // Android Testing Support Library's runner and rules
    androidTestCompile "com.android.support.test:runner:$rootProject.ext.runnerVersion"
    androidTestCompile "com.android.support.test:rules:$rootProject.ext.runnerVersion"
                                                                                                          
    //
    // Dependencies for Android unit tests
    androidTestCompile "junit:junit:$rootProject.ext.junitVersion"
    androidTestCompile "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
                                                                                                          
    //
    // Espresso UI Testing
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.ext.espressoVersion"
                                                                                                          
    //
    // Resolve conflicts between main and test APK:
    androidTestCompile "com.android.support:support-annotations:$rootProject.ext.supportLibraryVersion"
    androidTestCompile "com.android.support:support-v4:$rootProject.ext.supportLibraryVersion"
    androidTestCompile "com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion"
    androidTestCompile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    androidTestCompile "com.android.support:design:$rootProject.ext.supportLibraryVersion"
                                                                                                          
    androidTestCompile "com.google.dexmaker:dexmaker:$rootProject.ext.dexmakerVersion"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:$rootProject.ext.dexmakerVersion"
}